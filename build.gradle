plugins {
    id 'groovy'
    id 'maven-publish'
    id 'signing'
}

group = 'org.softwood'
version = '1.0-PRODUCTION'

repositories {
    mavenCentral()
}

java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(25)
    }
}
/*configurations.all {
    resolutionStrategy.force 'org.wiremock:wiremock:3.13.1'
}*/

//global variables defined in gradle.properties file
dependencies {
    implementation "org.apache.groovy:groovy:$groovyVersion"
    implementation("org.slf4j:slf4j-api:2.0.17")
    implementation("ch.qos.logback:logback-classic:1.5.20")

    testImplementation "org.spockframework:spock-core:$spockVersion"
    testImplementation platform('org.junit:junit-bom:6.0.1')
    testRuntimeOnly 'org.junit.platform:junit-platform-launcher'  //no longer pulled in automatically
    testImplementation 'org.junit.jupiter:junit-jupiter'
    testImplementation("net.bytebuddy:byte-buddy:1.17.8")

    // Jetty 12.0.x HTTP/2 + ALPN (compatible with WireMock's Jetty12 adapter)
    // Jetty 12 (HTTP/2 + ALPN + Servlet EE10)
    testImplementation "org.eclipse.jetty:jetty-server:$jettyVersion"
    testImplementation "org.eclipse.jetty:jetty-alpn-java-server:$jettyVersion"
    testImplementation "org.eclipse.jetty.http2:jetty-http2-server:$jettyVersion"
    testImplementation "org.eclipse.jetty.http2:jetty-http2-common:$jettyVersion"
    testImplementation "org.eclipse.jetty.ee10:jetty-ee10-servlet:$jettyVersion"


}

java {
    withSourcesJar()
    withJavadocJar()
}

test {
    useJUnitPlatform()
}

// Publishing configuration
publishing {
    publications {
        mavenJava(MavenPublication) {
            from components.java

            pom {
                name = project.name
                description = 'A Groovy library'
                url = 'https://github.com/woodmawa/groovy-HttpClient'

                licenses {
                    license {
                        name = 'The Apache License, Version 2.0'
                        url = 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                    }
                }

                developers {
                    developer {
                        id = 'woodmawa'
                        name = 'William Woodman'
                        email = 'will.woodman@outlook.com'
                    }
                }

                scm {
                    connection = 'scm:git:git://github.com/woodmawa/groovy-HttpClient.git'
                    developerConnection = 'scm:git:ssh://github.com/woodmawa/groovy-HttpClient.git'
                    url = 'https://github.com/woodmawa/groovy-HttpClient'
                }
            }
        }
    }

    // Configure repositories
    repositories {
        // Local Maven repository
        maven {
            name = 'localRepo'
            url = layout.buildDirectory.dir('repo')
        }

        // Remote Maven repository (conditional configuration based on credentials)
        // Remote Maven repository (conditional configuration based on credentials)
        maven {
            name = 'remoteRepo'
            url = project.hasProperty('remoteRepoUrl') ? project.property('remoteRepoUrl') : "https://repo.example.com/repository/maven-releases/"

            // Check if credentials are present before configuring them
            def user = project.findProperty('mavenUsername') ?: System.getenv('MAVEN_USERNAME')
            def pass = project.findProperty('mavenPassword') ?: System.getenv('MAVEN_PASSWORD')

            if (user && pass) {
                credentials {
                    username = user
                    password = pass
                }
            } else {
                // If credentials are missing, you can skip configuration
                // or use a flag to prevent the publish task from being created.
                // For now, simply finding the properties above is sufficient
                // as the publish task won't be created unless required properties are met.
            }
        }
    }
}

// Optional signing configuration for published artifacts
signing {
    required { gradle.taskGraph.hasTask("publish") }
    sign publishing.publications.mavenJava
}

// Task to publish to the local repository
tasks.register('publishToLocalRepo') {
    dependsOn 'publishMavenJavaPublicationToLocalRepoRepository'
    doLast {
        println "Published to local repository: ${layout.buildDirectory.dir('repo').get().asFile}"
    }
}

// Task to publish to the remote repository
tasks.register('publishToRemoteRepo') {
    dependsOn 'publishMavenJavaPublicationToRemoteRepoRepository'
    doLast {
        println "Published to remote repository"
    }
}

