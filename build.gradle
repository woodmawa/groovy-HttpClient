plugins {
    id 'groovy'
    id 'maven-publish'
    id 'signing'
}

group = 'org.softwood'
version = '1.0-SNAPSHOT'

repositories {
    mavenCentral()
}

/*configurations.all {
    resolutionStrategy.force 'org.wiremock:wiremock:3.13.1'
}*/

//global variables defined in gradle.properties file
dependencies {
    implementation group: 'org.apache.groovy', name: 'groovy', version: "$groovyVersion"
    implementation("org.slf4j:slf4j-api:2.0.17")
    implementation("ch.qos.logback:logback-classic:1.5.20")

    testImplementation group: 'org.spockframework', name: 'spock-core', version: "$spockVersion"
    testImplementation platform('org.junit:junit-bom:6.0.1')
    testRuntimeOnly 'org.junit.platform:junit-platform-launcher'  //no longer pulled in automatically
    testImplementation 'org.junit.jupiter:junit-jupiter'
    testImplementation("net.bytebuddy:byte-buddy:1.17.8")
    // Add WireMock for HTTP/2 testing
    testImplementation 'org.wiremock:wiremock-jetty12:3.13.1'
    //testImplementation("org.wiremock:wiremock-standalone:3.13.1") - causes problems

    // CRITICAL: Add Jetty ALPN dependencies for HTTP/2 over TLS to enable ALPN negotiation
    //testImplementation("org.eclipse.jetty:jetty-alpn-java-server:11.0.26")
    //testImplementation("org.eclipse.jetty:jetty-alpn-server:11.0.26")
/*    testImplementation 'org.eclipse.jetty:jetty-alpn-java-server:12.1.3'
    testImplementation 'org.eclipse.jetty.http2:jetty-http2-server:12.1.3'
    testImplementation 'org.eclipse.jetty.http2:jetty-http2-common:12.1.3'
    testImplementation 'org.eclipse.jetty:jetty-alpn-client:12.1.3'*/

    // Jetty 12.0.x HTTP/2 + ALPN (compatible with WireMock's Jetty12 adapter)
    // Jetty 12 (HTTP/2 + ALPN + Servlet EE10)
    testImplementation 'org.eclipse.jetty:jetty-server:12.0.9'
    testImplementation 'org.eclipse.jetty:jetty-alpn-java-server:12.0.9'
    testImplementation 'org.eclipse.jetty.http2:jetty-http2-server:12.0.9'
    testImplementation 'org.eclipse.jetty.http2:jetty-http2-common:12.0.9'
    testImplementation 'org.eclipse.jetty.ee10:jetty-ee10-servlet:12.0.9'


}

java {
    withSourcesJar()
    withJavadocJar()
}

test {
    useJUnitPlatform()
}

// Publishing configuration
publishing {
    publications {
        mavenJava(MavenPublication) {
            from components.java

            pom {
                name = project.name
                description = 'A Groovy library'
                url = 'https://github.com/yourusername/yourproject'

                licenses {
                    license {
                        name = 'The Apache License, Version 2.0'
                        url = 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                    }
                }

                developers {
                    developer {
                        id = 'woodmawa'
                        name = 'William Woodman'
                        email = 'will.woodman@outlook.com'
                    }
                }

                scm {
                    connection = 'scm:git:git://github.com/woodmawa/groovy-HttpClient.git'
                    developerConnection = 'scm:git:ssh://github.com/woodmawa/groovy-HttpClient.git'
                    url = 'https://github.com/woodmawa/groovy-HttpClient'
                }
            }
        }
    }

    // Configure repositories
    repositories {
        // Local Maven repository
        maven {
            name = 'localRepo'
            url = layout.buildDirectory.dir('repo')
        }

        // Remote Maven repository (conditional configuration based on credentials)
        maven {
            name = 'remoteRepo'
            url = project.hasProperty('remoteRepoUrl') ? project.property('remoteRepoUrl') : "https://repo.example.com/repository/maven-releases/"

            credentials {
                username = project.findProperty('mavenUsername') ?: System.getenv('MAVEN_USERNAME')
                password = project.findProperty('mavenPassword') ?: System.getenv('MAVEN_PASSWORD')
            }
        }
    }
}

// Optional signing configuration for published artifacts
signing {
    required { gradle.taskGraph.hasTask("publish") }
    sign publishing.publications.mavenJava
}

// Task to publish to the local repository
tasks.register('publishToLocalRepo') {
    dependsOn 'publishMavenJavaPublicationToLocalRepoRepository'
    doLast {
        println "Published to local repository: ${layout.buildDirectory.dir('repo').get().asFile}"
    }
}

// Task to publish to the remote repository
tasks.register('publishToRemoteRepo') {
    dependsOn 'publishMavenJavaPublicationToRemoteRepoRepository'
    doLast {
        println "Published to remote repository"
    }
}

